apply plugin: 'com.android.application'

android {

	signingConfigs {
		debug {
		}
		release {
		}
	}

	defaultConfig {
		applicationId rootProject.ext.applicationId
		minSdkVersion rootProject.ext.minSdkVersion
		targetSdkVersion rootProject.ext.targetSdkVersion
		versionCode rootProject.ext.versionCode
		versionName rootProject.ext.versionName

		multiDexEnabled true

		manifestPlaceholders = [CHANNEL_NAME: "DebugChannel"]
	}
	buildTypes {

		debug {
			buildConfigField "boolean", "LOG_DEBUG", "true"
			debuggable true
			minifyEnabled false
			signingConfig signingConfigs.debug
		}

		release {
			buildConfigField "boolean", "LOG_DEBUG", "false"
			debuggable false
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release

			applicationVariants.all { variant ->
				variant.outputs.each { output ->
					def outputFile = output.outputFile
					if (outputFile != null && outputFile.name.endsWith('.apk')) {
						def fileName = "网页防火墙_${rootProject.ext.versionName}_${variant.productFlavors[0].name}.apk"
						output.outputFile = new File(outputFile.parent, fileName)
					}
				}
			}
		}
	}

	productFlavors {
		DebugChannel {
			manifestPlaceholders = [CHANNEL_NAME: "DebugChannel"]
		}
		ThreeSixZero {
			manifestPlaceholders = [CHANNEL_NAME: "360Market"]
		}
		Baidu {
			manifestPlaceholders = [CHANNEL_NAME: "Baidu"]
		}
		NineOneMobile {
			manifestPlaceholders = [CHANNEL_NAME: "91Market"]
		}
		AndroidMarket {
			manifestPlaceholders = [CHANNEL_NAME: "AndroidMarket"]
		}
		Meizu {
			manifestPlaceholders = [CHANNEL_NAME: "Meizu"]
		}
		ShouGou {
			manifestPlaceholders = [CHANNEL_NAME: "ShouGou"]
		}
		WanDouJia {
			manifestPlaceholders = [CHANNEL_NAME: "WanDouJia"]
		}
		XiaoMi {
			manifestPlaceholders = [CHANNEL_NAME: "XiaoMi"]
		}
		YingYongBao {
			manifestPlaceholders = [CHANNEL_NAME: "YingYongBao"]
		}
		ThreeGMenHu {
			manifestPlaceholders = [CHANNEL_NAME: "3GMenHu"]
		}
		AnZhiMarket {
			manifestPlaceholders = [CHANNEL_NAME: "AnZhiMarket"]
		}
		Nduo {
			manifestPlaceholders = [CHANNEL_NAME: "Nduo"]
		}
	}
}

File propFile = file("sign.properties");
if (propFile.exists()) {
	def Properties props = new Properties();
	props.load(new FileInputStream(propFile));

	if (props.containsKey('keyAlias') && props.containsKey('keyPassword') &&
			props.containsKey('storeFile') && props.containsKey('storePassword')) {
		android.signingConfigs {
			debug {
				keyAlias props.get('keyAlias')
				keyPassword props.get('keyPassword')
				storeFile file(props.get('storeFile'))
				storePassword props.get('storePassword')
			}
			release {
				keyAlias props.get('keyAlias')
				keyPassword props.get('keyPassword')
				storeFile file(props.get('storeFile'))
				storePassword props.get('storePassword')
			}
		}
	} else {
		android.buildTypes.release.signingConfig = null
	}
} else {
	android.buildTypes.release.signingConfig = null
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	testCompile 'junit:junit:4.12'
	compile 'com.android.support:appcompat-v7:23.1.0'
	compile 'de.greenrobot:eventbus:2.4.0'
	//检查内存泄露状况
	debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3'
	releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3'
	compile 'com.nineoldandroids:library:2.4.0'
	compile 'com.android.support:design:23.1.0'

	//avoscloud-sdk 为 LeanCloud基础包
	compile 'cn.leancloud.android:avoscloud-sdk:v3.+'

	compile 'me.relex:circleindicator:1.1.8@aar'
}
